load("@io_bazel_rules_scala//scala:scala_cross_version_select.bzl", "select_for_scala_version")
load("@rules_jvm_external//:defs.bzl", "java_export")

scala_library(
    name = "api-lib",
    srcs = glob(["src/main/scala/ai/chronon/api/*.scala"]) +
           select_for_scala_version(
               before_2_12 = [
                   "//api/src/main/scala-2.11/scala/util:ScalaVersionSpecificCollectionsConverter.scala",
               ],
               between_2_12_and_2_13 = [
                   "//api/src/main/scala-2.12/scala/util:ScalaVersionSpecificCollectionsConverter.scala",
               ],
               since_2_13 = [
                   "//api/src/main/scala-2.13/scala/util:ScalaVersionSpecificCollectionsConverter.scala",
               ],
           ),
    visibility = ["//visibility:public"],
    deps = [
        ":api-models",
        "//third_party/java/spark:spark-libs",
        maven_artifact("com.fasterxml.jackson.core:jackson-core"),
        maven_artifact("com.fasterxml.jackson.core:jackson-databind"),
        maven_artifact("org.apache.thrift:libthrift"),
        maven_artifact("org.slf4j:slf4j-api"),
        maven_artifact("org.slf4j:slf4j-log4j12"),
    ] + select_for_scala_version(
        before_2_13 = [
            scala_artifact("org.scala-lang.modules:scala-collection-compat"),
        ],
        since_2_13 = [
            scala_artifact("org.scala-lang.modules:scala-parallel-collections"),
        ],
    ),
)

scala_test_suite(
    name = "test",
    srcs = glob(["src/test/scala/ai/chronon/api/test/*.scala"]),
    visibility = ["//visibility:public"],
    deps = [
        ":api-models",
        ":api-lib",
        "//third_party/java/spark:spark-libs",
        maven_artifact("com.fasterxml.jackson.core:jackson-core"),
        maven_artifact("com.fasterxml.jackson.core:jackson-databind"),
        maven_artifact("org.apache.thrift:libthrift"),
        maven_artifact("org.slf4j:slf4j-api"),
        maven_artifact("org.slf4j:slf4j-log4j12"),
        maven_artifact("junit:junit"),
        maven_artifact("com.novocode:junit-interface"),
        scala_artifact("org.scalatestplus:mockito-3-4"),
        maven_artifact("org.mockito:mockito-core"),
        scala_artifact(
            "org.scala-lang.modules:scala-collection-compat",
        ),
    ] + select_for_scala_version(
        since_2_13 = [
            maven_artifact("org.scala-lang.modules:scala-parallel-collections_2.13"),
        ],
    ),
)

java_library(
    name = "api-models",
    srcs = ["//api/thrift:api-models-java"],
    visibility = ["//visibility:public"],
    deps = [
        maven_artifact("javax.annotation:javax.annotation.api"),
        maven_artifact("org.apache.thrift:libthrift"),
    ],
)

genrule(
    name = "generate_pom",
    srcs = ["//:pom.xml.tpl"],
    outs = ["export-pom.tpl"],
    cmd = """
    cat $(location //:pom.xml.tpl) \\
        | sed 's/{name}/api/g' \\
        > $@
    """,
)

java_export(
    name = "api-export",
    maven_coordinates = "ai.chronon:api_(scala_version):$(version)",
    pom_template = ":generate_pom",
    runtime_deps = [
        ":api-lib",
    ],
)
